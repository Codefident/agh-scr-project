package trafficLights
public
	with Data_Model;
	with SEI;
	with urbanTraffic;
	with emergencyVehicle;
	
	-- DATA --
	data Signal
		properties
			Data_Model::Enumerators => ("Go", "Stop", "ChangingSignal", "Failure");
			Data_Size => 15 bytes;
	end Signal;
	
	data RequestSignal
		properties
			Data_Model::Data_Representation => Boolean;
			Data_Size => 2 bytes;
	end RequestSignal;
	
	data LightStatus
		properties
			Data_Model::Data_Representation => Boolean;
			Data_Size => 2 bytes;
	end LightStatus;
	
	
	-- SYSTEM --
	system TrafficLightsSystem
		features
			radioBus: requires bus access urbanTraffic::Radio;
			radioIn: in data port emergencyVehicle::EmergencySignal;
	end TrafficLightsSystem;
	
	system implementation TrafficLightsSystem.impl
		subcomponents
			
			-- lights
			mainCarLight: device CarTrafficLight;
			sideCarLight: device CarTrafficLight;
			tramLight: device TramTrafficLight;
			mainPedLight: device PedTrafficLight;
			sidePedLight: device PedTrafficLight;
			
			-- other devices
			pedButtonMain: device PedButton.impl;
			pedButtonSide: device PedButton.impl;
			tramSensor: device TramSensor.impl;
			radioReceiver: device RadioReceiver.impl;
			
			-- other components
			cpu: processor CPU.impl;
			mem: memory Mem.impl;
			commBus: bus CommBus.impl;
			
			-- processes
			mainController: process MainController.impl;
			signalRouter: process SignalRouter.impl;
			
			
		connections
			-- Pedestrian and tram request connections
			c1: port pedButtonMain.requestSignal -> mainController.receivePedMainRequest;
			c2: port pedButtonSide.requestSignal -> mainController.receivePedSideRequest;
			c3: port tramSensor.requestSignal -> mainController.receiveTramRequest;

			-- Main controller <-> router
			c4: port mainController.sendSignal -> signalRouter.controlIn;
			c5: port signalRouter.statusOut -> mainController.receiveLightStatus;
			
			-- Light Status to signal router
			c6: port mainCarLight.sendStatus -> signalRouter.mainCarStatus;
			c7: port sideCarLight.sendStatus -> signalRouter.sideCarStatus;
			c8: port tramLight.sendStatus -> signalRouter.tramStatus;
			c9: port mainPedLight.sendStatus -> signalRouter.mainPedStatus;
			c10: port sidePedLight.sendStatus -> signalRouter.sidePedStatus;
			
			-- Light Signal to lights
			c11: port signalRouter.mainCarSignal -> mainCarLight.signalReceive;
			c12: port signalRouter.sideCarSignal -> sideCarLight.signalReceive;
			c13: port signalRouter.tramSignal -> tramLight.signalReceive;
			c14: port signalRouter.mainPedSignal -> mainPedLight.signalReceive;
			c15: port signalRouter.sidePedSignal -> sidePedLight.signalReceive;
			
			-- Emergency signal
			c16: port radioReceiver.outEmergencySignal -> mainController.receiveEmergencySignal;
			c17: port radioIn -> radioReceiver.radioIn;
			
			-- Bus
			b1: bus access pedButtonMain.busConn <-> commBus;
			b2: bus access pedButtonSide.busConn <-> commBus;
			b3: bus access tramSensor.busConn <-> commBus;
			b4: bus access mainCarLight.busConn <-> commBus;
			b5: bus access sideCarLight.busConn <-> commBus;
			b6: bus access tramLight.busConn <-> commBus;
			b7: bus access mainPedLight.busConn <-> commBus;
			b8: bus access sidePedLight.busConn <-> commBus;
			bcpu: bus access cpu.busConn <-> commBus;
			bmem: bus access mem.busConn <-> commBus;
			brecRadio: bus access radioBus -> radioReceiver.radioBus;
			brecComm: bus access radioReceiver.busConn <-> commBus;
			
		-- Actual bindings
		properties
			Actual_Processor_Binding => (reference(cpu)) applies to mainController;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.pedMainButtonThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.pedSideButtonThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.tramSensorThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.radioReceiverThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.lightThread;
		
			Actual_Memory_Binding => (reference(mem)) applies to mainController;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.pedMainButtonThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.pedSideButtonThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.tramSensorThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.radioReceiverThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.lightThread;
		
			Actual_Connection_Binding => (reference(commBus)) applies to pedButtonMain.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to pedButtonSide.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to tramSensor.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to mainCarLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to sideCarLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to tramLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to mainPedLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to sidePedLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to cpu.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to mem.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to radioReceiver.busConn;
			
	end TrafficLightsSystem.impl;
	
	
	-- PROCESSES --
	process MainController
		features
			receivePedMainRequest: in data port RequestSignal;
			receivePedSideRequest: in data port RequestSignal;
			receiveTramRequest: in data port RequestSignal;
			receiveLightStatus: in data port LightStatus;
			receiveEmergencySignal: in data port emergencyVehicle::EmergencySignal;
			sendSignal: out data port Signal;
		properties
			SEI::MIPSBudget => 1.3 mips;
	end MainController;
	
	process implementation MainController.impl
		subcomponents
			pedMainButtonThread: thread PedButtonThread.impl;
			pedSideButtonThread: thread PedButtonThread.impl;
			tramSensorThread: thread TramSensorThread.impl;
			radioReceiverThread: thread RadioReceiverThread.impl;
			lightThread: thread LightThread.impl;
		connections
			c1: port receiveTramRequest -> tramSensorThread.dataIn;
			c2: port receivePedMainRequest -> pedMainButtonThread.dataIn;
			c3: port receivePedSideRequest -> pedSideButtonThread.dataIn;
			c4: port lightThread.dataOut -> sendSignal;
			c5: port receiveLightStatus -> lightThread.dataIn;
			c6: port receiveEmergencySignal -> radioReceiverThread.dataIn;
	end MainController.impl;
	
	process SignalRouter
	    features
	        -- Inputs from each light
	        mainCarStatus: in data port LightStatus;
	        sideCarStatus: in data port LightStatus;
	        tramStatus: in data port LightStatus;
	        mainPedStatus: in data port LightStatus;
	        sidePedStatus: in data port LightStatus;
	
	        -- Aggregated output to MainController
	        statusOut: out data port LightStatus;
	
	        -- Signal from MainController
	        controlIn: in data port Signal;
	
	        -- Fan-out to each light
	        mainCarSignal: out data port Signal;
	        sideCarSignal: out data port Signal;
	        tramSignal: out data port Signal;
	        mainPedSignal: out data port Signal;
	        sidePedSignal: out data port Signal;
	end SignalRouter;

	process implementation SignalRouter.impl
	    subcomponents
	        routerThread: thread SignalRouterThread.impl;
	    connections
	        -- Map light statuses to internal thread
	        s1: port mainCarStatus -> routerThread.inMainCar;
	        s2: port sideCarStatus -> routerThread.inSideCar;
	        s3: port tramStatus -> routerThread.inTram;
	        s4: port mainPedStatus -> routerThread.inMainPed;
	        s5: port sidePedStatus -> routerThread.inSidePed;
	
	        -- Aggregate status to main controller
	        s6: port routerThread.outStatus -> statusOut;
	
	        -- Signal in from controller
	        s7: port controlIn -> routerThread.inSignal;
	
	        -- Forward signals to lights
	        s8: port routerThread.outMainCar -> mainCarSignal;
	        s9: port routerThread.outSideCar -> sideCarSignal;
	        s10: port routerThread.outTram -> tramSignal;
	        s11: port routerThread.outMainPed -> mainPedSignal;
	        s12: port routerThread.outSidePed -> sidePedSignal;
	end SignalRouter.impl;
	
	
	-- THREADS --
	thread PedButtonThread
		features
			dataIn: in data port RequestSignal;
	end PedButtonThread;
	thread implementation PedButtonThread.impl
	end PedButtonThread.impl;
	
	thread TramSensorThread
		features
			dataIn: in data port RequestSignal;
	end TramSensorThread;
	thread implementation TramSensorThread.impl
	end TramSensorThread.impl;
	
	thread LightThread
		features
			dataOut: out data port Signal;
			dataIn: in data port LightStatus;
	end LightThread;
	thread implementation LightThread.impl
	end LightThread.impl;
	
	thread RadioReceiverThread
		features
			dataIn: in data port emergencyVehicle::EmergencySignal;
			dataOut: out data port emergencyVehicle::EmergencySignal;
	end RadioReceiverThread;
	thread implementation RadioReceiverThread.impl
	end RadioReceiverThread.impl;
	
	thread SignalRouterThread
	    features
	        inMainCar: in data port LightStatus;
	        inSideCar: in data port LightStatus;
	        inTram: in data port LightStatus;
	        inMainPed: in data port LightStatus;
	        inSidePed: in data port LightStatus;
	        
	        inSignal: in data port Signal;
	        
	        outMainCar: out data port Signal;
	        outSideCar: out data port Signal;
	        outTram: out data port Signal;
	        outMainPed: out data port Signal;
	        outSidePed: out data port Signal;
	        
	        outStatus: out data port LightStatus;
	end SignalRouterThread;
	
	thread implementation SignalRouterThread.impl
	end SignalRouterThread.impl;
	
	
	-- PROCESSOR --
	processor CPU
		features
			busConn: requires bus access CommBus;
		properties
			SEI::MIPSCapacity => 120.0 mips;
	end CPU;
	processor implementation CPU.impl
	end CPU.impl;
	
	-- MEMORY --
	memory Mem
		features
			busConn: requires bus access CommBus;
	end Mem;
	memory implementation Mem.impl
	end Mem.impl;
	
	
	-- BUS --
	bus CommBus
		properties
			SEI::BandWidthBudget => 420.0 KBytesps;
			SEI::BandWidthCapacity => 3.0 MBytesps;
	end CommBus;
	bus implementation CommBus.impl
	end CommBus.impl;
	
	
	-- DEVICES --
	device CarTrafficLight
		features
			signalReceive: in data port Signal;
			sendStatus: out data port LightStatus;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 18.0 kg;
	end CarTrafficLight;
	device implementation CarTrafficLight.impl
	end CarTrafficLight.impl;
	
	device TramTrafficLight
		features
			signalReceive: in data port Signal;
			sendStatus: out data port LightStatus;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 14.0 kg;
	end TramTrafficLight;
	device implementation TramTrafficLight.impl
	end TramTrafficLight.impl;
	
	device PedTrafficLight
		features
			signalReceive: in data port Signal;
			sendStatus: out data port LightStatus;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 16.0 kg;
	end PedTrafficLight;
	device implementation PedTrafficLight.impl
	end PedTrafficLight.impl;
	
	device PedButton
		features
			requestSignal: out data port RequestSignal;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 0.7 kg;
	end PedButton;
	device implementation PedButton.impl
	end PedButton.impl;
	
	device TramSensor
		features
			requestSignal: out data port RequestSignal;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 10.0 kg;
	end TramSensor;
	device implementation TramSensor.impl
	end TramSensor.impl;
	
	device RadioReceiver
		features
			outEmergencySignal: out data port emergencyVehicle::EmergencySignal;
			radioIn: in data port emergencyVehicle::EmergencySignal;
			radioBus: requires bus access urbanTraffic::Radio;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 0.1 kg;
	end RadioReceiver;
	device implementation RadioReceiver.impl
	end RadioReceiver.impl;
	
end trafficLights;
