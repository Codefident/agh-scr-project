package trafficLights
public
	with Data_Model;
	with SEI;
	with urbanTraffic;
	with emergencyVehicle;
	
	-- DATA --
	data Signal
		properties
			Data_Model::Enumerators => ("Go", "Stop", "ChangingSignal", "Failure");
			Data_Size => 15 bytes;
	end Signal;
	
	data RequestSignal
		properties
			Data_Model::Data_Representation => Boolean;
			Data_Size => 2 bytes;
	end RequestSignal;
	
	data LightStatus
		properties
			Data_Model::Data_Representation => Boolean;
			Data_Size => 2 bytes;
	end LightStatus;
	
	
	-- SYSTEM --
	system TrafficLightsSystem
		features
			radioBus: requires bus access urbanTraffic::Radio;
			radioIn: in data port emergencyVehicle::EmergencySignal;
	end TrafficLightsSystem;
	
	system implementation TrafficLightsSystem.impl
		subcomponents
			
			-- lights
			mainCarLight: device CarTrafficLight;
			sideCarLight: device CarTrafficLight;
			tramLight: device TramTrafficLight;
			mainPedLight: device PedTrafficLight;
			sidePedLight: device PedTrafficLight;
			
			-- other devices
			pedButtonMain: device PedButton.impl;
			pedButtonSide: device PedButton.impl;
			tramSensor: device TramSensor.impl;
			radioReceiver: device RadioReceiver.impl;
			
			-- other components
			cpu: processor CPU.impl;
			mem: memory Mem.impl;
			commBus: bus CommBus.impl;
			
			-- processes
			mainController: process MainController.impl;
			
			
		connections
			-- Pedestrian and tram request connections
			c1: port pedButtonMain.requestSignal -> mainController.receivePedMainRequest;
			c2: port pedButtonSide.requestSignal -> mainController.receivePedSideRequest;
			c3: port tramSensor.requestSignal -> mainController.receiveTramRequest;

			-- Signal output from controller to lights
			c4: port mainController.sendMainCarSignal -> mainCarLight.signalReceive;
			c5: port mainController.sendSideCarSignal -> sideCarLight.signalReceive;
			c6: port mainController.sendTramSignal -> tramLight.signalReceive;
			c7: port mainController.sendMainPedSignal -> mainPedLight.signalReceive;
			c8: port mainController.sendSidePedSignal -> sidePedLight.signalReceive;
			
			-- Light Status
			c9: port mainCarLight.sendStatus -> mainController.receiveMainCarStatus;
			c10: port sideCarLight.sendStatus -> mainController.receiveSideCarStatus;
			c11: port tramLight.sendStatus -> mainController.receiveTramStatus;
			c12: port mainPedLight.sendStatus -> mainController.receiveMainPedStatus;
			c13: port sidePedLight.sendStatus -> mainController.receiveSidePedStatus;
			
			-- Emergency signal
			c14: port radioReceiver.outEmergencySignal -> mainController.receiveEmergencySignal;
			c15: port radioIn -> radioReceiver.radioIn;
			
			-- Bus
			b1: bus access pedButtonMain.busConn <-> commBus;
			b2: bus access pedButtonSide.busConn <-> commBus;
			b3: bus access tramSensor.busConn <-> commBus;
			b4: bus access mainCarLight.busConn <-> commBus;
			b5: bus access sideCarLight.busConn <-> commBus;
			b6: bus access tramLight.busConn <-> commBus;
			b7: bus access mainPedLight.busConn <-> commBus;
			b8: bus access sidePedLight.busConn <-> commBus;
			bcpu: bus access cpu.busConn <-> commBus;
			bmem: bus access mem.busConn <-> commBus;
			brecRadio: bus access radioBus -> radioReceiver.radioBus;
			brecComm: bus access radioReceiver.busConn <-> commBus;
			
		-- Actual bindings
		properties
			Actual_Processor_Binding => (reference(cpu)) applies to mainController;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.pedMainButtonThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.pedSideButtonThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.tramSensorThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.radioReceiverThread;
			Actual_Processor_Binding => (reference(cpu)) applies to mainController.lightThread;
		
			Actual_Memory_Binding => (reference(mem)) applies to mainController;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.pedMainButtonThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.pedSideButtonThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.tramSensorThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.radioReceiverThread;
			Actual_Memory_Binding => (reference(mem)) applies to mainController.lightThread;
		
			Actual_Connection_Binding => (reference(commBus)) applies to pedButtonMain.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to pedButtonSide.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to tramSensor.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to mainCarLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to sideCarLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to tramLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to mainPedLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to sidePedLight.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to cpu.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to mem.busConn;
			Actual_Connection_Binding => (reference(commBus)) applies to radioReceiver.busConn;
			
	end TrafficLightsSystem.impl;
	
	
	-- PROCESSES --
	process MainController
		features
			receivePedMainRequest: in data port RequestSignal;
			receivePedSideRequest: in data port RequestSignal;
			receiveTramRequest: in data port RequestSignal;
			
			receiveMainCarStatus: in data port LightStatus;
			receiveSideCarStatus: in data port LightStatus;
			receiveTramStatus: in data port LightStatus;
			receiveMainPedStatus: in data port LightStatus;
			receiveSidePedStatus: in data port LightStatus;

			receiveEmergencySignal: in data port emergencyVehicle::EmergencySignal;
			
			sendMainCarSignal: out data port Signal;
			sendSideCarSignal: out data port Signal;
			sendTramSignal: out data port Signal;
			sendMainPedSignal: out data port Signal;
			sendSidePedSignal: out data port Signal;
		properties
			SEI::MIPSBudget => 1.3 mips;
	end MainController;
	
	process implementation MainController.impl
		subcomponents
			pedMainButtonThread: thread PedButtonThread.impl;
			pedSideButtonThread: thread PedButtonThread.impl;
			tramSensorThread: thread TramSensorThread.impl;
			radioReceiverThread: thread RadioReceiverThread.impl;
			lightThread: thread LightThread.impl;
		connections
			c1: port receiveTramRequest -> tramSensorThread.dataIn;
			c2: port receivePedMainRequest -> pedMainButtonThread.dataIn;
			c3: port receivePedSideRequest -> pedSideButtonThread.dataIn;
			
			c4: port lightThread.sendMainCarSignal -> sendMainCarSignal;
			c5: port lightThread.sendSideCarSignal -> sendSideCarSignal;
			c6: port lightThread.sendTramSignal -> sendTramSignal;
			c7: port lightThread.sendMainPedSignal -> sendMainPedSignal;
			c8: port lightThread.sendSidePedSignal -> sendSidePedSignal;
			
			c9: port receiveMainCarStatus -> lightThread.receiveMainCarStatus;
			c10: port receiveSideCarStatus -> lightThread.receiveSideCarStatus;
			c11: port receiveTramStatus -> lightThread.receiveTramStatus;
			c12: port receiveMainPedStatus -> lightThread.receiveMainPedStatus;
			c13: port receiveSidePedStatus -> lightThread.receiveSidePedStatus;

			c14: port receiveEmergencySignal -> radioReceiverThread.dataIn;
	end MainController.impl;
	
	
	-- THREADS --
	thread PedButtonThread
		features
			dataIn: in data port RequestSignal;
	end PedButtonThread;
	thread implementation PedButtonThread.impl
	end PedButtonThread.impl;
	
	thread TramSensorThread
		features
			dataIn: in data port RequestSignal;
	end TramSensorThread;
	thread implementation TramSensorThread.impl
	end TramSensorThread.impl;
	
	thread LightThread
		features
			sendMainCarSignal: out data port Signal;
			sendSideCarSignal: out data port Signal;
			sendTramSignal: out data port Signal;
			sendMainPedSignal: out data port Signal;
			sendSidePedSignal: out data port Signal;
			
			receiveMainCarStatus: in data port LightStatus;
			receiveSideCarStatus: in data port LightStatus;
			receiveTramStatus: in data port LightStatus;
			receiveMainPedStatus: in data port LightStatus;
			receiveSidePedStatus: in data port LightStatus;
	end LightThread;
	thread implementation LightThread.impl
	end LightThread.impl;
	
	thread RadioReceiverThread
		features
			dataIn: in data port emergencyVehicle::EmergencySignal;
			dataOut: out data port emergencyVehicle::EmergencySignal;
	end RadioReceiverThread;
	thread implementation RadioReceiverThread.impl
	end RadioReceiverThread.impl;
	
	-- PROCESSOR --
	processor CPU
		features
			busConn: requires bus access CommBus;
		properties
			SEI::MIPSCapacity => 120.0 mips;
	end CPU;
	processor implementation CPU.impl
	end CPU.impl;
	
	-- MEMORY --
	memory Mem
		features
			busConn: requires bus access CommBus;
	end Mem;
	memory implementation Mem.impl
	end Mem.impl;
	
	
	-- BUS --
	bus CommBus
		properties
			SEI::BandWidthBudget => 420.0 KBytesps;
			SEI::BandWidthCapacity => 3.0 MBytesps;
	end CommBus;
	bus implementation CommBus.impl
	end CommBus.impl;
	
	
	-- DEVICES --
	device CarTrafficLight
		features
			signalReceive: in data port Signal;
			sendStatus: out data port LightStatus;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 18.0 kg;
	end CarTrafficLight;
	device implementation CarTrafficLight.impl
	end CarTrafficLight.impl;
	
	device TramTrafficLight
		features
			signalReceive: in data port Signal;
			sendStatus: out data port LightStatus;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 14.0 kg;
	end TramTrafficLight;
	device implementation TramTrafficLight.impl
	end TramTrafficLight.impl;
	
	device PedTrafficLight
		features
			signalReceive: in data port Signal;
			sendStatus: out data port LightStatus;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 16.0 kg;
	end PedTrafficLight;
	device implementation PedTrafficLight.impl
	end PedTrafficLight.impl;
	
	device PedButton
		features
			requestSignal: out data port RequestSignal;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 0.7 kg;
	end PedButton;
	device implementation PedButton.impl
	end PedButton.impl;
	
	device TramSensor
		features
			requestSignal: out data port RequestSignal;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 10.0 kg;
	end TramSensor;
	device implementation TramSensor.impl
	end TramSensor.impl;
	
	device RadioReceiver
		features
			outEmergencySignal: out data port emergencyVehicle::EmergencySignal;
			radioIn: in data port emergencyVehicle::EmergencySignal;
			radioBus: requires bus access urbanTraffic::Radio;
			busConn: requires bus access CommBus;
		properties
			SEI::GrossWeight => 0.1 kg;
	end RadioReceiver;
	device implementation RadioReceiver.impl
	end RadioReceiver.impl;
	
end trafficLights;